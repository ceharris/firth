; !        x a-addr --   STORE cell in memory
CODE "!",1,0,STORE
    ppop H,L
    writeWord HL, B,C
    PDROP          ; POP new TOS
    NEXT

CODE "@",1,0,AT
    getTOS H,L ; HL = TOS
    readWord HL, B,C
    NEXT

; C!      char c-addr --    STORE char in memory
CODE "c!",2,0,CSTORE
    ppop H,L
    ld (HL),C
    PDROP          ; POP new TOS
    NEXT

; C@     c-addr -- char   AT char from memory
CODE "c@",2,0,CAT
    ld A,(BC)
    ld C,A
    ld B,0
    NEXT

;   R@          ( -- w )
;               Copy top of return stack to the data stack.
CODE	'R@',2,0,RAT
	PUSH	BC
	LD	C,(IX+0)
	LD	B,(IX+1)
	NEXT

;z RSP!  a-addr --                  set return stack pointer
CODE "rp!",4,0,RPSTO
    getTOS H,L
    LD SP, HL
    PDROP                      ; POP TOS
    NEXT

;   RP@         ( -- a )
;               Push the current RP to the data stack
CODE 'rp@', 3,0,RPAT
    PDUP                      ; PUSH TOS
    LD HL,0
    ADD HL,SP 
    setTOS H,L
    NEXT

CODE "sp@",3,0,SPAT
    push IX
    pop HL
    ppush H,L
    NEXT

CODE "sp!",3,0,SPSTORE
    ppop H,L
    push HL
    pop IX
    NEXT

; allocate n bytes of memory                                   ( n -- ) allocate n bytes of memory
CODE "allot",5,0,allot               ;
    ENTER
    ; dw here, addstore                   ; adds n to here
    dw here, PSTOR                   ; adds n to here
    EXIT

;   +!          ( n a -- )
;               Add n to the contents at address a.
CODE  '+!',2,0,PSTOR
    ENTER
    DW      SWAP,OVER,AT,PLUS
    DW      SWAP,STORE,EXIT

CODE ",",1,0,COMMA
	ld HL, (VAR_HERE)	; HERE
	writeWord HL, B,C
	ld (VAR_HERE), HL	; Update HERE (incremented)
	PDROP		        ; cleanup TOS
    NEXT

CODE "c,",1,0,CCOMMA
    PTUCK H,L
	ld HL, (VAR_HERE)	; HERE
	ld (HL), C          ; STORE DE at memory pointed to by VAR_HERE
	inc HL
	ld (VAR_HERE), HL	; Update HERE (incremented)
    PNIP H,L
	PDROP		        ; cleanup TOS
    NEXT

